//! # Array
//!
//! `#import "std/array"`
//!
//! Compiler builtin dynamic array is automatically growing memory storage allocated on heap. The
//! array allocated memory block groves over time when we push new values to the array. It's
//! guaranteed that all pushed elements are stored in single continuous block of memory. When there
//! is no space reminding in currently allocated block, new one is allocated and all already pushed
//! data are copied from old block to the new one.
//!
//! ```bl
//! array :: struct {
//!     len:             s64;
//!     ptr:            *u8;
//!     allocated_elems: usize;
//!     allocator:      *Allocator;
//! }
//! ```
//!
//! ### Example
//!
//! ```bl
//! @@@examples/dynamic_arrays.bl
//! ```
//!
//! ## Preallocation Rules
//!
//! Underlying array buffer is allocated using provided allocator or `application_context.allocator`
//! by default.
//!
//! - In case the array does not hold any previous allocation, at least `ARRAY_ALLOC_BLOCK_SIZE`
//!   bytes are preallocated.
//! - In case the array already holds preallocated buffer and new requested size fits into current
//!   preallocated block, no allocation is done.
//! - In case the array already holds preallocated buffer which is not large enought to hold
//!   requested data size `ARRAY_GROW_FACTOR * requested size` is preallocated.

ARRAY_ALLOC_BLOCK_SIZE : usize = 32;
ARRAY_GROW_FACTOR      : f32   = 1.5f;

// Not direcly used internally, when layout of this structure is changed, internal compiler
// representation must be modified too.
_Array :: struct { // @Clanup 2025-02-12: We don't need this anymore!
	len: s64;
	ptr: *u8;
	allocated_elems: usize;
	allocator: *Allocator;
}

/// Sets dynamic array to default state. If array contains already allocated memory `array_terminate`
/// must be called first.
///
/// Initial capacity can be specified by `capacity` (count of elements). If `capacity` is greater
/// than 0, needed memory is preallocated using specified `allocator`; otherwise no allocation is done.
///
/// It's usually enough to rely on default implicit initialization in case we don't need specify
/// custom allocator or preallocate storage to specified `capacity` of entries.
array_init :: fn (_arr: *[..]?T, capacity: s64 = 0, allocator: *Allocator = null) #inline {
	assert(_arr, "Passed array is invalid.");
	arr := cast(*_Array) _arr;
	arr.ptr = null;
	arr.len = 0;
	arr.allocated_elems = 0;
	arr.allocator = allocator;
	array_reserve(_arr, capacity);
}

/// Release memory allocated by dynamic array. This function is supposed to be called when dynamic
/// array is no longer needed. Passed dynamic array is reset to default state.
/// Calling `array_terminate` on already terminated or non-initialized array is allowed.
array_terminate :: fn (_arr: *[..]?T) #inline {
	arr := cast(*_Array) _arr;
	free(arr.ptr, arr.allocator);
	arr.ptr = null;
	arr.len = 0;
	arr.allocated_elems = 0;
}

/// Append new value at the dynamic array end. New heap allocation is done here only in cases when
/// there is not enough memory allocated to store all values.
/// Returns pointer to the new appended element.
///
/// !!! note
///     When there is no element to push provided, function will just allocate memory for
///     a new empty element without any initialization.
array_push :: fn { impl_push_empty; impl_push; }

#scope_private
impl_push :: fn (arr: *[..]?T, v: T) *T #inline {
	data      :: v;
	elem_size :: sizeof(T);
	dest      :: impl_push_empty(arr);
	memcpy(auto dest, auto &data, elem_size);
	return dest;
}

impl_push_empty :: fn (arr: *[..]?T) *T {
	elem_size :: sizeof(T);
	arr.len += 1;
	_array_ensure_space(auto arr, elem_size, auto arr.len, alignof(T));
	return auto _array_get_elem_ptr(auto arr, elem_size, arr.len - 1);
}
#scope_public

/// Append all `data` at the dynamic array end. New heap allocation is done here only in cases when
/// there is not enough memory allocated to store all values.
/// Does nothing when `data.len < 1`.
// @Incomplete 2025-02-12: Document what empty values does???
array_push_all :: fn (_arr: *[..]?T, data: []T) {
	arr := cast(*_Array) _arr;
	if data.len < 1 then return;
	elem_size :: sizeof(T);
	arr.len += data.len;
	_array_ensure_space(auto arr, elem_size, auto arr.len, alignof(T));
	dest_ptr :: _array_get_elem_ptr(auto arr, elem_size, arr.len - data.len);
	memcpy(dest_ptr, auto data.ptr, elem_size * auto data.len);
}

/// Duplicate the last array element into `out` argument (if not null) and reduce the array length by
/// one. Returns `true` in case the element was erased.
array_pop :: fn (_arr: *[..]?T, out: *T = null) bool #inline {
	arr := cast(*_Array) _arr;
	// Eventually assert?
	if arr.len == 0 { return false; }
	if out {
		elem :: _array_get_elem_ptr(arr, sizeof(T), arr.len - 1);
		memcpy(auto out, elem, sizeof(T));
	}
	arr.len -= 1;
	return true;
}

/// Duplicate first array element into `out` location (if not null) and shift all other elements by
/// one slot left (the ordeing is kept). Returns `true` in case the element was erased.
array_pop_first :: fn (_arr: *[..]?T, out: *T = null) bool #inline {
	arr := cast(*_Array) _arr;
	// Eventually assert?
	if arr.len == 0 { return false; }
	if out {
		elem :: _array_get_elem_ptr(arr, sizeof(T), 0);
		memcpy(auto out, elem, sizeof(T));
	}
	array_erase_keep_order(_arr, 0);
	return true;
}

/// Reserve heap memory for `n` elements in array. Does nothing in case the already allocated block is
/// large enough to hold `n` elements, the allocated memory block might be bigger then requested size.
///
/// In case `n` is 0 function immediately returns without any allocations.
array_reserve :: fn (_arr: *[..]?T, n: s64) #inline {
	arr := cast(*_Array) _arr;
	if n == 0 { return; }
	if n < 0 { panic("Invalid array capacity!"); }
	_array_ensure_space(arr, sizeof(T), auto n, alignof(T));
}

array_resize :: fn (_arr: *[..]?T, len: s64) #inline {
	array_reserve(_arr, len);
	_arr.len = len;
}

/// Erase element on index `i`. Call to this function can cause reordering of an dynamic array.
/// Allocated memory is kept even if we erase entire dynamic array. Function invoke panic in case
/// of index overflow. @Incomplete 2025-02-12: Info about range-check?
array_erase :: fn (_arr: *[..]?T, i: s64) {
	arr := cast(*_Array) _arr;
	if i >= arr.len || i < 0 {
		panic("Array index out of range, index is % but array size is %.", i, arr.len);
	}

	// Remove last element.
	if i == arr.len - 1 {
		arr.len -= 1;
		return;
	}
	elem_size :: sizeof(T);
	memcpy(
		_array_get_elem_ptr(arr, elem_size, i),
		_array_get_elem_ptr(arr, elem_size, arr.len - 1),
		elem_size
	);
	arr.len -= 1;
}

/// Erase element on index `i` and move all following elements left. This might be slow in case the array is
/// large. @Incomplete 2025-02-12: Info about range-check?
array_erase_keep_order :: fn (_arr: *[..]?T, i: s64) {
	arr := cast(*_Array) _arr;
	if i >= arr.len || i < 0 {
		panic("Array index out of range, index is % but array size is %.", i, arr.len);
	}

	if i == arr.len - 1 {
		arr.len -= 1;
		return;
	}
	elem_size :: sizeof(T);
	size_to_move :: (arr.len - i - 1) * auto elem_size;
	memmove(_array_get_elem_ptr(arr, elem_size, i), _array_get_elem_ptr(arr, elem_size, i + 1), auto size_to_move);
	arr.len -= 1;
}

/// Erase element of the value `v` and return `true` in case element was erased. Note this might be expensive
/// since all elements in array might be visited in the worst case.
array_erase_value :: fn (arr: *[..]?T, v: T) bool {
	loop i := 0; i < arr.len; i += 1 {
		if (@arr)[i] == v {
			array_erase(arr, i);
			return true;
		}
	}
	return false;
}

// @Incomplete 2025-02-12: Panic instead of assert on invalid range. Document this!
/// Insert a new element into the array before position `before_index` and return index of the new
/// inserted element. All elements after the new one are moved in the memory by one slot.
///
/// Value of `before_index` must be in range <0, arr.len>. Inserting at `before_index` = 0 basically
/// adds a new element at the array begin; insering at `before_index` = arr.len is the same as
/// `array_push`.
///
/// Order of already existing elements is preserved.
array_insert :: fn (_arr: *[..]?T, before_index: s64, value: T) s64 {
	return array_insert_all(_arr, before_index, []T.{ 1, &value });
}

// @Incomplete 2025-02-12: Document! + what empty values does??? What is returned???
// @Incomplete 2025-02-12: Missing tests!
array_insert_all :: fn (_arr: *[..]?T, before_index: s64, values: []T) s64 {
	assert(_arr, "Passed array is invalid.");
	arr := cast(*_Array) _arr;
	if before_index < 0 || before_index > arr.len {
		panic("Array index out of range, index value is supposed to be in range <0, %>, provided index is %.", arr.len, before_index);
	}
	if values.len < 1 then return before_index;

	if before_index == arr.len {
		// Last (insert before the end).
		array_push_all(_arr, values);
		return before_index;
	}

	elem_size    :: sizeof(T);
	size_to_move :: (arr.len - before_index) * auto elem_size;

	// Allocate new space at the end of array.
	len :: arr.len + values.len;
	_array_ensure_space(arr, elem_size, auto len, alignof(T));
	arr.len = len;

	memmove(
		_array_get_elem_ptr(arr, elem_size, before_index + values.len),
		_array_get_elem_ptr(arr, elem_size, before_index),
		auto size_to_move
	);
	memcpy(
		_array_get_elem_ptr(arr, elem_size, before_index),
		auto values.ptr,
		elem_size * auto values.len
	);

	return before_index;
}

// @Cleanup: We tend to use directly arr.len = 0 in our code base, so we can remove this function eventually...
/// Erase all elements in dynamic array but keep allocated memory.
array_clear :: fn (_arr: *[..]?T) #inline #obsolete "Use 'arr.len = 0' instead. [Since 0.13.0]" {
	arr := cast(*_Array) _arr;
	arr.len = 0;
}

array_allocated_bytes :: fn (_arr: *[..]?T) usize #inline {
	arr :: cast(*_Array) _arr;
	return arr.allocated_elems;
}

// Mostly implementation private stuff, this is not private because it might be useful in some rare cases.
// However it's preffered to use standard public api.

_array_get_elem_ptr :: fn (arr: *_Array, elem_size: usize, i: s64) *u8 #inline {
	if i >= arr.len then panic("Element index out of range, index is % but array size is %.", i, arr.len);
	return ptr_shift_bytes(arr.ptr, i * cast(s64) elem_size);
}

_array_ensure_space :: fn (arr: *_Array, elem_size: usize, needed_elems: usize, alignement: usize) {
	if needed_elems == 0                   { return; }
	if arr.allocated_elems >= needed_elems { return; }

	// Allocated larger block for later use.
	extended_needed_elems := needed_elems;
	if arr.allocated_elems == 0 {
		extended_needed_elems = max(ARRAY_ALLOC_BLOCK_SIZE, extended_needed_elems);
	} else {
		extended_needed_elems = auto (ARRAY_GROW_FACTOR * auto extended_needed_elems);
	}

	assert(extended_needed_elems >= needed_elems);

	needed_bytes :: extended_needed_elems * elem_size;
	mem, err :: realloc(arr.ptr, needed_bytes, alignement, arr.allocator);
	if err { panic(err); }

	arr.ptr = mem;
	arr.allocated_elems = extended_needed_elems;
}

#scope_private
#import "std/math"
#import "std/print"