#import "std/print"
#import "std/fs"

// This function will load provided file and tries to print its content. Error is returned
// in case of any failure.
print_file_content :: fn (filepath: string_view) Error {
	content :: read_entire_file(filepath) catch return $;
	defer free_slice(&content);
	print(content);

	return OK;
}

div :: fn (a: s32, b: s32) (s32, Error) {
	if b == 0 {
		// Return error with message.
		return 0, error("Divide by zero '%/%'!", a, b);
	}
	// Return result and OK state.
	return a / b, OK;
}


main :: fn () s32 {

	// Call print_file_content, the catch branch is executed in case there is an
	// error returned by the function.
	print_file_content("some_invalid_file_path") catch {
		// Use $ builtin symbol to access the captured error.
		print_err($);
	};

	print_file_content(#file) catch {
		print_err($);
	};

	// In case of multi-return value, error is expected to be last in the return
	// list.
	result :: div(10, 0) catch {
		print_err($);
	};
	print("result = %\n", result);

	return 0;
}
